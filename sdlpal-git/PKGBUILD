# Maintainer:  Shaw <puxx@mail.ustc.edu.cn>

pkgname=sdlpal-git
pkgver=20140216
pkgrel=1
pkgdesc="A SDL-based reimplemention of the classic Chinese-language RPG 'Xian Jian Qi Xia Zhuan'"
arch=('i686' 'x86_64')
url="http://sdlpal.codeplex.com/"
license=('GPL')
depends=('sdl' 'xdg-utils')
makedepends=('git' 'imagemagick')
install="sdlpal.install"
source=("sdlpal::git+https://git01.codeplex.com/sdlpal"
        sdlpal.sh
        sdlpal.desktop
        sdlpal.xpm)
md5sums=('SKIP'
         'ebab8d35405e7a2b917542c1cfa1bb5c'
         '3d08c1d5ad6c433620b1a8b61e6b9176'
         '02f75b1da809795f0dad3c97f51eea1f')

build() {
	cd "${srcdir}/sdlpal/sdlpal"
	make
}

package() {
	cd "${srcdir}"
	mkdir -p "${pkgdir}/usr/share/sdlpal"
	# install executable files
	install -Dm755 "sdlpal.sh" "${pkgdir}/usr/bin/sdlpal"
	install -Dm755 "${srcdir}/sdlpal/sdlpal/sdlpal" "${pkgdir}/usr/share/sdlpal/sdlpal"
	# install desktop file
	install -Dm644 "sdlpal.desktop" "${pkgdir}/usr/share/applications/sdlpal.desktop"
	# install icons
	for size in 16 22 24 32 48 64; do
		mkdir -p "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps"
		convert -resize "${size}" sdlpal.xpm "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/sdlpal.png"
	done
	install -Dm644 sdlpal.xpm "${pkgdir}/usr/share/icons/scalable/apps/sdlpal.xpm"
}

