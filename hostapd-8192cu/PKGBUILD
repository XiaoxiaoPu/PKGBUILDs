# Maintainer:  Shaw <puxx@mail.ustc.edu.cn>

pkgname=hostapd-8192cu
pkgver=0.8_rtw_r7475.20130812_beta
pkgrel=2
pkgdesc="IEEE 802.11 AP, IEEE 802.1X/WPA/WPA2/EAP/RADIUS Authenticator, compatible with Realtek RTL8188CUS (8188C, 8192C) chipset wireless cards"
arch=('i686' 'x86_64' 'armv6h')
url="http://www.realtek.com.tw/"
license=('GPL2')
depends=('glibc')
provides=('hostapd')
conflicts=('hostapd')
backup=('etc/hostapd/hostapd.conf'
        'etc/hostapd/hlr_auc_gw.milenage_db'
        'etc/hostapd/hostapd.accept'
        'etc/hostapd/hostapd.deny'
        'etc/hostapd/hostapd.eap_user'
        'etc/hostapd/hostapd.radius_clients'
        'etc/hostapd/hostapd.sim_db'
        'etc/hostapd/hostapd.vlan'
        'etc/hostapd/hostapd.wpa_psk'
        'etc/hostapd/wired.conf')
install=hostapd.install
source=('https://github.com/XiaoXiaoPu/hostapd-8192cu/raw/master/RTL8188C_8192C_USB_linux_v4.0.2_9000.20130911.zip'
        'hostapd.service'
        'config')
md5sums=('5f15575ba6ae58cae485e8060bc771ed'
         'a0a16879eed5e4e41ae6b225a4809955'
         '3aedf1122829df5a3b32e4de67969a3e')

build() {
	cd "${srcdir}/RTL8188C_8192C_USB_linux_v4.0.2_9000.20130911"
	cd wpa_supplicant_hostapd
	tar -zxf wpa_supplicant_hostapd-0.8_rtw_r7475.20130812.tar.gz \
	    -C "${srcdir}/"
	cd "${srcdir}/wpa_supplicant_hostapd-0.8_rtw_r7475.20130812"
	cd hostapd
	cp "${srcdir}/config" .config
	make
}

package() {
	cd "${srcdir}/wpa_supplicant_hostapd-0.8_rtw_r7475.20130812"
	cd hostapd
	install -Dm755 hostapd "${pkgdir}/usr/bin/hostapd"
	install -Dm755 hostapd_cli "${pkgdir}/usr/bin/hostapd_cli"
	mkdir -p "${pkgdir}/etc/hostapd"
	install -m644 hlr_auc_gw.milenage_db hostapd.accept \
	    hostapd.conf hostapd.deny hostapd.eap_user \
	    hostapd.radius_clients hostapd.sim_db \
	    hostapd.vlan hostapd.wpa_psk wired.conf \
	    "${pkgdir}/etc/hostapd/"
	install -Dm644 hostapd_cli.1 "${pkgdir}/usr/share/man/man1/hostapd_cli.1"
	install -Dm644 hostapd.8 "${pkgdir}/usr/share/man/man8/hostapd.8"
	install -Dm644 "${srcdir}/hostapd.service" \
	    "${pkgdir}/usr/lib/systemd/system/hostapd.service"
	cd "${srcdir}/RTL8188C_8192C_USB_linux_v4.0.2_9000.20130911"
	cd wpa_supplicant_hostapd
	install -m644 rtl_hostapd_2G.conf rtl_hostapd_5G.conf \
	    "${pkgdir}/etc/hostapd/"
}

