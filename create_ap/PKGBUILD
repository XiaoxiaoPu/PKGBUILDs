# Maintainer:  Xiaoxiao Pu <i@xiaoxiao.im>

pkgname=create_ap
pkgver=git~20140921
pkgrel=1
pkgdesc="A shell script to create a NATed/Bridged Software Access Point"
arch=('any')
url="https://github.com/oblique/create_ap"
license=('BSD')
depends=('bash' 'hostapd' 'iproute2' 'iw' 'dnsmasq'
         'iptables' 'bridge-utils')
optdepends=('haveged: boost low entropy')
install=create_ap.install
_commit=ec0df440f82e7a0daca10dc1b12a1c85bb47b9d7
source=("https://github.com/oblique/create_ap/archive/${_commit}.tar.gz")
sha256sums=('62fd6f51eea28dc75d8ffe8f4b05591d63442ed282d4c56bdff50f8b80dd148c')

build() {
	cd "${srcdir}/create_ap-${_commit}/"
	sed -i 's/#!\/bin\/bash/#!\/usr\/bin\/env bash/g' create_ap
}

package() {
	cd "${srcdir}/create_ap-${_commit}/"
	install -Dm755 "create_ap" "${pkgdir}/usr/bin/create_ap"
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/create_ap/LICENSE"
	install -Dm644 "create_ap.service" "${pkgdir}/usr/share/doc/create_ap/create_ap.service"
}