# Maintainer:  Xiaoxiao Pu <i@xiaoxiao.im>

pkgname=create_ap
pkgver=git~20141124
pkgrel=1
pkgdesc="A shell script to create a NATed/Bridged Software Access Point"
arch=('any')
url="https://github.com/oblique/create_ap"
license=('BSD')
depends=('bash' 'hostapd' 'iproute2' 'iw' 'dnsmasq'
         'iptables' 'bridge-utils')
optdepends=('haveged: boost low entropy')
install=create_ap.install
_commit=814ddf91006aaf983b9afe2c89b88c19415d8093
source=("https://github.com/oblique/create_ap/archive/${_commit}.tar.gz")
sha256sums=('9b7bbd21e30084b9fbb3f5649bfb630b4b0525f91053d3c49e27d2e5670a7b20')

prepare() {
	cd "${srcdir}/create_ap-${_commit}/"
	sed -i 's/#!\/bin\/bash/#!\/usr\/bin\/env bash/g' create_ap
}

package() {
	cd "${srcdir}/create_ap-${_commit}/"
	install -Dm755 "create_ap" "${pkgdir}/usr/bin/create_ap"
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/create_ap/LICENSE"
	install -Dm644 "create_ap.service" "${pkgdir}/usr/share/doc/create_ap/create_ap.service"
}