# Maintainer:  Xiaoxiao Pu <i@xiaoxiao.im>

pkgname=create_ap
pkgver=git~20141104
pkgrel=1
pkgdesc="A shell script to create a NATed/Bridged Software Access Point"
arch=('any')
url="https://github.com/oblique/create_ap"
license=('BSD')
depends=('bash' 'hostapd' 'iproute2' 'iw' 'dnsmasq'
         'iptables' 'bridge-utils')
optdepends=('haveged: boost low entropy')
install=create_ap.install
_commit=f831ad98d6496d5ab037055de0d11dd5e5413544
source=("https://github.com/oblique/create_ap/archive/${_commit}.tar.gz")
sha256sums=('2e6d0ab7951413ea67f2556dd8e0ed52fda8d7ace5491d64311f7fbe6de40198')

prepare() {
	cd "${srcdir}/create_ap-${_commit}/"
	sed -i 's/#!\/bin\/bash/#!\/usr\/bin\/env bash/g' create_ap
}

package() {
	cd "${srcdir}/create_ap-${_commit}/"
	install -Dm755 "create_ap" "${pkgdir}/usr/bin/create_ap"
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/create_ap/LICENSE"
	install -Dm644 "create_ap.service" "${pkgdir}/usr/share/doc/create_ap/create_ap.service"
}