# Maintainer:  Shaw <puxx@mail.ustc.edu.cn>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>

pkgname=haroopad
pkgver=0.11.0
pkgrel=1
pkgdesc="A markdown enabled document processor"
arch=('i686' 'x86_64')
url="http://pad.haroopress.com/"
license=('unknown')
depends=('xdg-utils'
         'desktop-file-utils'
         'hicolor-icon-theme'
         'gconf'
         'alsa-lib'
         'gtk2'
         'nss'
         'libudev.so.0')
options=('!strip')
install=${pkgname}.install
changelog=changelog
source=("https://www.dropbox.com/s/fhmy9fssd8m0hrr/${pkgname}-v${pkgver}-i386.deb"
	    "Haroopad.desktop"
	    "x-markdown.xml")
md5sums=('2ceba060dd80afc0895e141b81a97ab2'
         'a49c9e82add99942182eb9b47c34f509'
         '438bce645323cb0f0959c548c8c7daf0')

PKGEXT=.pkg.tar

if [[ ${CARCH} = x86_64 ]]; then
	source[0]="https://www.dropbox.com/s/qroboui3on1kxu2/${pkgname}-v${pkgver}_amd64.deb"
	md5sums[0]='64e9f9bc53cd2096f7bce5d053565192'
fi

build() {
	cd "${srcdir}"
	tar -zxf data.tar.gz
}

package() {
	cd "${srcdir}"
	cp -r "${srcdir}/usr" "${pkgdir}/usr"
	rm "${pkgdir}/usr/share/applications/mimeapps.list"
	rm "${pkgdir}/usr/share/applications/Haroopad.desktop"
	mkdir "${pkgdir}/usr/share/doc/haroopad/"
	mv "${pkgdir}/usr/share/doc/changelog" \
	   "${pkgdir}/usr/share/doc/copyright" \
	   "${pkgdir}/usr/share/doc/haroopad/"
	install -Dm644 x-markdown.xml "${pkgdir}/usr/share/mime/packages/x-markdown.xml"
	install -Dm644 Haroopad.desktop "${pkgdir}/usr/share/applications/Haroopad.desktop"
}

